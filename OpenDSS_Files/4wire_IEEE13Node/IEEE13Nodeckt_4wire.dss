Clear
Set DefaultBaseFrequency=60

!
! This script is based on a script developed by Tennessee Tech Univ students
! Tyler Patton, Jon Wood, and David Woods, April 2009
! Modified by Andres Avila - February 2024
! This version is developed considering the cable parameters. No kron's reduction

new circuit.IEEE13Nodeckt_3phMod_v1_NEV
~ basekv=115 pu=1.0001 phases=3 bus1=SourceBus  
~ Angle=30                                                         ! advance angle 30 deg so result agree with published angle
~ MVAsc3=20000 MVASC1=21000    ! stiffen the source to approximate inf source

!Set earthmodel = carson

!SUB TRANSFORMER DEFINITION 
! Although this data was given, it does not appear to be used in the test case results
! The published test case starts at 1.0 per unit at Bus 650. To make this happen, we will change the impedance
! on the transformer to something tiny by dividing by 1000 using the DSS in-line RPN math
New Transformer.Sub Phases=3 Windings=2   XHL=(8 1000 /)
~ wdg=1 bus=SourceBus   conn=delta  kv=115  kva=5000   %r=(.5 1000 /) 
~ wdg=2 bus=650           conn=wye    kv=4.16  kva=5000   %r=(.5 1000 /)  

! REACTOR FOR TRANSFORMER IN SUBSTATION
! New Reactor.Neutral phases=1 bus1=650.4 bus2=650.0 R=0.00001 X=0  

! FEEDER 1-PHASE VOLTAGE REGULATORS
! Define low-impedance 2-wdg transformer

New Transformer.Reg1 phases=1 bank=reg1 XHL=0.01 kVAs=[1666 1666]
~ Buses=[650.1 RG60.1] kVs=[2.4  2.4] %LoadLoss=0.01
new regcontrol.Reg1  transformer=Reg1 winding=2  vreg=122  band=2  ptratio=20 ctprim=700  R=3   X=9 

New Transformer.Reg2 phases=1 bank=reg1 XHL=0.01 kVAs=[1666 1666]
~ Buses=[650.2 RG60.2] kVs=[2.4  2.4] %LoadLoss=0.01
new regcontrol.Reg2  transformer=Reg2 winding=2  vreg=122  band=2  ptratio=20 ctprim=700  R=3   X=9 

New Transformer.Reg3 phases=1 bank=reg1 XHL=0.01 kVAs=[1666 1666]
~ Buses=[650.3 RG60.3] kVs=[2.4  2.4] %LoadLoss=0.01
new regcontrol.Reg3  transformer=Reg3 winding=2  vreg=122  band=2  ptratio=20 ctprim=700  R=3   X=9 

! REACTOR FOR REGULATOR
! New Reactor.RG60   phases=1 bus1=RG60.4    Bus2=RG60.0   R=0.00001 X=0


!TRANSFORMER DEFINITION 
New Transformer.XFM1  Phases=3   Windings=2  XHL=2
~ wdg=1 bus=633       conn=Wye kv=4.16    kva=500    %r=.55 
~ wdg=2 bus=634       conn=Wye kv=0.480    kva=500    %r=.55

! REACTOR FOR TRANSFORMER XFM1
! New Reactor.NEUTRAL_XFM1 phases=1 bus1=634.4 bus2=634.0 R=0 X=0  


!WIREDATA DEFINITION
!Overhead wires
New WireData.ACSR_556_500	Runits=mi	Rac=0.1859	GMRunits=ft	GMRac=0.0313	Radunits=in Diam=0.927
New WireData.ACSR_4/0 		Runits=mi	Rac=0.592	GMRunits=ft GMRac=0.00814	Radunits=in Diam=0.563
New WireData.ACSR_1/0 		Runits=mi	Rac=1.12	GMRunits=ft GMRac=0.00446	Radunits=in	Diam=0.398
!Underground wires
New CNDATA.AA_250_CN	NormAmps=260	Rac=0.41	Runits=mi	GMRac=0.0171	GMRUnits=ft	Radius=0.2835	Radunits=in DiaCable=1.16 DiaIns=1.06
	~ Rstrand=14.8722	GmrStrand=0.00208	DiaStrand=0.0641	InsLayer=0.23	k=13	EpsR=2.3
	
! Data obtained from https://sourceforge.net/p/electricdss/code/HEAD/tree/trunk/Test/IEEELineGeometry.dss	
New TSDATA.AA_1/0_TS	diashield=0.85	tapelayer=0.005	epsR=2.3 
	~ inslayer=0.22	DiaIns=0.78	Rdc=0.184204546	GMRac=0.13320	diam=0.368
	~ Runits=kft	Radunits=in	GMRunits=in

New WireData.CU_1/0 Runits=mi	Rac=0.607	GMRunits=ft GMRac=0.01113	Radunits=in	Diam=0.368


!LINEGEOMETRY DEFINITION

New LineGeometry.ID500_ACSR_556_500	nconds=4	nphases=3	reduce=n
~ cond=1 wire=ACSR_556_500	units=ft	x=-3.5	h=28
~ cond=2 wire=ACSR_556_500	units=ft	x=-1	h=28
~ cond=3 wire=ACSR_556_500	units=ft	x=3.5	h=28
~ cond=4 wire=ACSR_4/0		units=ft	x=0.5	h=24
New LineGeometry.ID500_ACSR_4/0	nconds=4	nphases=3	reduce=n
	~ cond=1 wire=ACSR_4/0	units=ft	x=-3.5	h=28
	~ cond=2 wire=ACSR_4/0	units=ft	x=-1	h=28
	~ cond=3 wire=ACSR_4/0	units=ft	x=3.5	h=28
	~ cond=4 wire=ACSR_4/0	units=ft	x=0.5	h=24
New LineGeometry.ID505	nconds=3	nphases=2	reduce=n
	~ cond=1 wire=ACSR_1/0	units=ft	x=-3.5	h=28
	~ cond=2 wire=ACSR_1/0	units=ft	x=3.5	h=28
	~ cond=3 wire=ACSR_1/0	units=ft	x=0.5	h=24
New LineGeometry.ID510	nconds=2	nphases=1	reduce=n
	~ cond=1 wire=ACSR_1/0	units=ft	x=0		h=29
	~ cond=2 wire=ACSR_1/0	units=ft	x=0.5	h=24
New LineGeometry.ID515	nconds=3	nphases=3	reduce=n
	~ cond=1 cncable=AA_250_CN	units=in	x=0		h=-48
	~ cond=2 cncable=AA_250_CN	units=in	x=6		h=-48
	~ cond=3 cncable=AA_250_CN	units=in	x=12	h=-48
New LineGeometry.ID520	nconds=2	nphases=1	reduce=n
	~ cond=1 tscable=AA_1/0_TS	units=in	x=0	h=-48
	~ cond=2 wire=CU_1/0		units=in	x=1	h=-48

!Bus 670 is the concentrated point load of the distributed load on line 632 to 671 located at 1/3 the distance from node 632

!LINE DEFINITIONS 

New Line.650632    Bus1=RG60.1.2.3.4  Bus2=632.1.2.3.4  geometry=ID500_ACSR_556_500 Length=2000 units=ft 
New Line.632670    Bus1=632.2.1.3.4    Bus2=670.2.1.3.4  geometry=ID500_ACSR_556_500 Length=667  units=ft    
New Line.670671    Bus1=670.2.1.3.4    Bus2=671.2.1.3.4  geometry=ID500_ACSR_556_500 Length=1333 units=ft 
New Line.671680    Bus1=671.2.1.3.4    Bus2=680.2.1.3.4  geometry=ID500_ACSR_556_500 Length=1000 units=ft 
New Line.632633    Bus1=632.3.1.2.4    Bus2=633.3.1.2.4  geometry=ID500_ACSR_4/0 Length=500  units=ft 
New Line.632645    Bus1=632.3.2.4      Bus2=645.3.2.4    geometry=ID505 Length=500  units=ft 
New Line.645646    Bus1=645.3.2.4      Bus2=646.3.2.4    geometry=ID505 Length=300  units=ft
New Line.692675    Bus1=692.1.2.3.4    Bus2=675.1.2.3.4  geometry=ID515 Length=500  units=ft 
New Line.671684    Bus1=671.1.3.4      Bus2=684.1.3.4    geometry=ID505 Length=300  units=ft 
New Line.684611    Bus1=684.3.4        Bus2=611.3.4      geometry=ID510 Length=300  units=ft 
New Line.684652    Bus1=684.1.4        Bus2=652.1.4      geometry=ID520 Length=800  units=ft

! REACTOR DEFINITIONS
/*
New Reactor.632    phases=1 bus1=632.4     Bus2=632.0    R=25 X=0
New Reactor.670    phases=1 bus1=670.4     Bus2=670.0    R=25 X=0
New Reactor.671    phases=1 bus1=671.4     Bus2=671.0    R=25 X=0
New Reactor.680    phases=1 bus1=680.4     Bus2=680.0    R=25 X=0
New Reactor.633    phases=1 bus1=633.4     Bus2=633.0    R=25 X=0
New Reactor.645    phases=1 bus1=645.4     Bus2=645.0    R=25 X=0
New Reactor.646    phases=1 bus1=646.4     Bus2=646.0    R=25 X=0
New Reactor.675    phases=1 bus1=675.4     Bus2=675.0    R=25 X=0
New Reactor.684    phases=1 bus1=684.4     Bus2=684.0    R=25 X=0
New Reactor.611    phases=1 bus1=611.4     Bus2=611.0    R=25 X=0
New Reactor.652    phases=1 bus1=652.4     Bus2=652.0    R=25 X=0
*/

!SWITCH DEFINITIONS 
!New Line.671692    Phases=3 Bus1=671   Bus2=692  Switch=y  r1=1e-4 r0=1e-4 x1=0.000 x0=0.000 c1=0.000 c0=0.000 !CLOSED
New Line.671692    Phases=3 Bus1=671.11.12.13   Bus2=692.1.2.3  Switch=y  r1=1e-4 r0=1e-4 x1=0.000 x0=0.000 c1=0.000 c0=0.000

!LOAD DEFINITIONS 
New Load.671 Bus1=671.1.2.3  Phases=3 Conn=Delta Model=1 kV=4.16   kW=1155 kvar=660 
New Load.634a Bus1=634.1.4     Phases=1 Conn=Wye  Model=1 kV=0.277  kW=160   kvar=110 
New Load.634b Bus1=634.2.4     Phases=1 Conn=Wye  Model=1 kV=0.277  kW=120   kvar=90 
New Load.634c Bus1=634.3.4     Phases=1 Conn=Wye  Model=1 kV=0.277  kW=120   kvar=90 
New Load.645 Bus1=645.2.4      Phases=1 Conn=Wye  Model=1 kV=2.4      kW=170   kvar=125
New Load.646 Bus1=646.2.3    Phases=1 Conn=Delta Model=2 kV=4.16    kW=230   kvar=132 
New Load.692 Bus1=692.3.1    Phases=1 Conn=Delta Model=5 kV=4.16    kW=170   kvar=151 
New Load.675a Bus1=675.1.4    Phases=1 Conn=Wye  Model=1 kV=2.4  kW=485   kvar=190 
New Load.675b Bus1=675.2.4    Phases=1 Conn=Wye  Model=1 kV=2.4  kW=68   kvar=60 
New Load.675c Bus1=675.3.4    Phases=1 Conn=Wye  Model=1 kV=2.4  kW=290   kvar=212 
New Load.611 Bus1=611.3.4      Phases=1 Conn=Wye  Model=5 kV=2.4  kW=170   kvar=80
New Load.652 Bus1=652.1.4      Phases=1 Conn=Wye  Model=2 kV=2.4  kW=128   kvar=86
New Load.670a Bus1=670.1.4    Phases=1 Conn=Wye  Model=1 kV=2.4  kW=17    kvar=10 
New Load.670b Bus1=670.2.4    Phases=1 Conn=Wye  Model=1 kV=2.4  kW=66    kvar=38 
New Load.670c Bus1=670.3.4    Phases=1 Conn=Wye  Model=1 kV=2.4  kW=117  kvar=68 

!CAPACITOR DEFINITIONS
New Capacitor.Cap1 Bus1=675 phases=3 kVAR=600 kV=4.16 
New Capacitor.Cap2 Bus1=611.3 phases=1 kVAR=100 kV=2.4 

Set Voltagebases=[115, 4.16, .48]
! calcv
! Solve
// BusCoords IEEE13Node_BusXY.csv

!---------------------------------------------------------------------------------------------------------------------------------------------------
!----------------Show some Results -----------------------------------------------------------------------------------------------------------------
!---------------------------------------------------------------------------------------------------------------------------------------------------


// Show Voltages LN Nodes
// Show Currents Elem
// Show Powers kVA Elem
// Show Losses
// Show Taps

!---------------------------------------------------------------------------------------------------------------------------------------------------
!---------------------------------------------------------------------------------------------------------------------------------------------------
! Alternate Solution Script
! To force the taps to be same as published results, set the transformer taps manually and disable the controls
!---------------------------------------------------------------------------------------------------------------------------------------------------
/*
Transformer.Reg1.Taps=[1.0 1.0625]
Transformer.Reg2.Taps=[1.0 1.0500]
Transformer.Reg3.Taps=[1.0 1.06875]
Set Controlmode=OFF

Solve
*/